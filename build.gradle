plugins {
    id "java"
	id "java-library"
	id "maven-publish"
	id "signing"
}

allprojects {
	group "de.cronn"
	version = System.env.ARTIFACT_VERSION ?: "SNAPSHOT"

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	compileJava.options.encoding = 'UTF-8'

	repositories {
		mavenCentral()
	}

	dependencies {
		// Filter release candidates and beta versions
		components.all { ComponentMetadataDetails details ->
			if (details.id.version =~ /(?i).+[-.](CANDIDATE|RC|BETA|ALPHA|M\d+).*/) {
				details.status = 'milestone'
			}
		}
	}

	dependencyLocking {
		lockAllConfigurations()
	}
}

java {
	withSourcesJar()
	withJavadocJar()

	registerFeature("h2Support") {
		usingSourceSet(sourceSets.main)
	}
}

test {
	useJUnitPlatform()
}

dependencies {
    api "org.junit.jupiter:junit-jupiter-api:latest.release"
	implementation "org.apache.commons:commons-lang3:latest.release"
	implementation "org.slf4j:slf4j-api:latest.release"

	h2SupportImplementation "org.springframework:spring-beans:latest.release"
	// Restrict Jakarta Persistence API to 2.x. With 3.x we need to change the packages to jakarta.persistence
	h2SupportImplementation "jakarta.persistence:jakarta.persistence-api:[2.2,3.0)"

	testImplementation "org.assertj:assertj-core:latest.release"
	testImplementation "org.mockito:mockito-core:latest.release"
	testImplementation "org.junit.platform:junit-platform-launcher:latest.release"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:latest.release"
	testRuntimeOnly "ch.qos.logback:logback-classic:latest.release"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId project.group
			artifactId project.name
			version project.version
			from components.java

			pom {
				name = project.name
				description = "Cronn test utils"
				url = "https://github.com/cronn/test-utils"

				licenses {
					license {
						name = "The Apache Software License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}

				developers {
					developer {
						name = "Radosław Postołowicz"
						email = "radoslaw.postolowicz@cronn.de"
					}
				}

				scm {
					url = "https://github.com/cronn/test-utils"
				}

				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}

		}
	}
	repositories {
		maven {
			url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = project.hasProperty('nexusUsername') ? project.property('nexusUsername') : System.getenv('NEXUS_USERNAME')
				password = project.hasProperty('nexusPassword') ? project.property('nexusPassword') : System.getenv('NEXUS_PASSWORD')
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
