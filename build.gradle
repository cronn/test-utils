plugins {
    id "java"
	id "java-library"
	id "maven-publish"
	id "signing"
	id "io.spring.dependency-management" version "1.1.0"
}

allprojects {
	group "de.cronn"
	version = System.env.ARTIFACT_VERSION ?: "SNAPSHOT"

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	compileJava.options.encoding = 'UTF-8'

	repositories {
		mavenCentral()
	}
}

java {
	withSourcesJar()
	withJavadocJar()

	registerFeature("h2Support") {
		usingSourceSet(sourceSets.main)
	}
	registerFeature("springSupport") {
		usingSourceSet(sourceSets.main)
	}
}

test {
	useJUnitPlatform()
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-dependencies:2.6.14"
	}
	generatedPomCustomization {
		enabled = false
	}
}

dependencies {
    api "org.junit.jupiter:junit-jupiter-api"
	implementation "org.apache.commons:commons-lang3"
	implementation "org.slf4j:slf4j-api"

	h2SupportImplementation "org.springframework:spring-beans"
	h2SupportImplementation "jakarta.persistence:jakarta.persistence-api"

	springSupportImplementation "org.springframework:spring-context"
	springSupportImplementation "org.springframework:spring-test"

	testImplementation "org.assertj:assertj-core"
	testImplementation "org.mockito:mockito-core"
	testImplementation "org.junit.platform:junit-platform-launcher"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
	testRuntimeOnly "ch.qos.logback:logback-classic"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId project.group
			artifactId project.name
			version project.version
			from components.java

			pom {
				name = project.name
				description = "Cronn test utils"
				url = "https://github.com/cronn/test-utils"

				licenses {
					license {
						name = "The Apache Software License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}

				developers {
					developer {
						name = "Radosław Postołowicz"
						email = "radoslaw.postolowicz@cronn.de"
					}
				}

				scm {
					url = "https://github.com/cronn/test-utils"
				}

				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}

		}
	}
	repositories {
		maven {
			url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = project.hasProperty('nexusUsername') ? project.property('nexusUsername') : System.getenv('NEXUS_USERNAME')
				password = project.hasProperty('nexusPassword') ? project.property('nexusPassword') : System.getenv('NEXUS_PASSWORD')
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
